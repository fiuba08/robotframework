*** Variables ***
${existing} =  foo
${LOG GOT WRONG ARGS} =  Keyword 'BuiltIn.Log' expected 1 to 2 arguments, got
${LOG GOT ZERO ARGS} =  ${LOG GOT WRONG ARGS} 0.
${LOG GOT THREE ARGS} =  ${LOG GOT WRONG ARGS} 3.


*** Test Cases ***
Run Keyword With Keyword with Invalid Number of Arguments
    [Documentation]  FAIL ${LOG GOT ZERO ARGS}
    Run Keyword    Log

Run Keyword With Missing Keyword
    [Documentation]  FAIL No keyword with name 'Missing' found.
    Run Keyword    Missing

Run Keyword With Variable In Keyword Name
    Run Keyword    Log ${missing}
    Run Keyword    Log ${existing}
    Run Keyword    Log ${EMPTY}

Run Keyword With UK
    Run Keyword  UK

Run Keyword With Failing UK
    [Documentation]  FAIL ${LOG GOT ZERO ARGS}
    Run Keyword  Failing UK

Comment
    Comment  Missing Keyword  Should Not Fail  Even Missing ${variable} Should Not Fail

Set Variables
    Set Test Variable  ${test}
    Set Suite Variable  ${suite}
    Set Suite Variable  ${global}
    Set Variable If  ${global}

Run Keywords When All Keywords Pass
    Run Keywords  Fail  No Operation  UK

Run Keywords When One Keyword Fails
    [Documentation]  FAIL ${LOG GOT ZERO ARGS}
    Run Keywords  Fail  No Operation  Log  UK

Run Keywords When Multiple Keyword Fails
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) ${LOG GOT ZERO ARGS}\n\n
    ...  2) No keyword with name 'Missing' found.
    Run Keywords  Fail  No Operation  Log  UK  Missing

Run Keywords With Arguments When All Keywords Pass
    Run Keywords  Log Many  this  is  AND  No Operation

Run Keywords With Arguments When One Keyword Fails
    [Documentation]  FAIL  Keyword 'BuiltIn.Log' expected 1 to 2 arguments, got 3.
    Run Keywords  Log  FOO  AND  Log  Bar  DEBUG  this should fail

Run Keywords With Arguments When Multiple Keyword Fails
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) Keyword 'BuiltIn.Log' expected 1 to 2 arguments, got 3.\n\n
    ...  2) No keyword with name 'no kw' found.
    Run Keywords  Log  FOO  DEBUG  this should fail  AND  no kw

Run Keyword in For Loop Pass
    :FOR  ${i}  IN RANGE  5
    \  Run Keyword  No Operation

Run Keyword in For Loop Fail
    [Documentation]  FAIL ${LOG GOT ZERO ARGS}
    :FOR  ${i}  IN RANGE  5
    \  Run Keyword  Log

Wait Until Keyword Succeeds Pass
    Wait Until Keyword Succeeds  30 seconds  1 second  No Operation

Wait Until Keyword Succeeds Fail
    [Documentation]  FAIL ${LOG GOT ZERO ARGS}
    Wait Until Keyword Succeeds  30 seconds  1 second  Log

Run Keyword If Pass
    Run Keyword If  ${TRUE}  No Operation

Run Keyword If Fail
    [Documentation]  FAIL ${LOG GOT ZERO ARGS}
    Run Keyword If  ${FALSE}  Log

Run Keyword If with ELSE
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) ${LOG GOT ZERO ARGS}\n\n
    ...  2) Keyword 'UK' expected 0 arguments, got 1.\n\n
    ...  3) No keyword with name 'Non Existing' found.\n\n
    ...  4) ${LOG GOT THREE ARGS}
    Run Keyword If    expression    No Operation    ELSE    UK
    RunKeywordIf      expression    Log             ELSE    No Operation
    runkeywordif      expression    No operation    ELSE    UK    not allowed
    RUN_KEYWORD_IF    expression    Non Existing    ELSE    Log    1   2   3

Run Keyword If with ELSE IF
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) ${LOG GOT ZERO ARGS}\n\n
    ...  2) Keyword 'UK' expected 0 arguments, got 1.\n\n
    ...  3) No keyword with name 'Non Existing' found.\n\n
    ...  4) ${LOG GOT THREE ARGS}
    Run Keyword If    expr    No Operation    ELSE IF    expr    UK
    Run Keyword If    expr    Log             ELSE IF    expr    No Operation
    Run Keyword If    expr    No operation    ELSE IF    expr    UK    not allowed
    Run Keyword If    expr    Non Existing    ELSE IF    expr    Log    1   2   3

Run Keyword If with ELSE IF and ELSE
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) ${LOG GOT ZERO ARGS}\n\n
    ...  2) Keyword 'UK' expected 0 arguments, got 2.\n\n
    ...  3) No keyword with name 'not found kw' found.\n\n
    ...  4) ${LOG GOT THREE ARGS}
    Run Keyword If    expr    Log
    ...    ELSE IF    expr    UK    1    2
    ...    ELSE IF    expr    not found kw
    ...    ELSE    Log    1   2   3

Run Keyword If with ELSE IF and ELSE without keywords
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) Invalid 'Run Keyword If' usage.\n\n
    ...  2) Invalid 'Run Keyword If' usage.\n\n
    ...  3) Invalid 'Run Keyword If' usage.\n\n
    ...  4) Invalid 'Run Keyword If' usage.\n\n
    ...  5) Invalid 'Run Keyword If' usage.\n\n
    ...  6) Invalid 'Run Keyword If' usage.\n\n
    ...  7) Invalid 'Run Keyword If' usage.\n\n
    ...  8) Invalid 'Run Keyword If' usage.\n\n
    ...  9) Invalid 'Run Keyword If' usage.\n\n
    ...  10) Invalid 'Run Keyword If' usage.\n\n
    ...  11) Keyword 'BuiltIn.Run Keyword If' expected at least 2 arguments, got 1.\n\n
    ...  12) Keyword 'BuiltIn.Run Keyword If' expected at least 2 arguments, got 1.\n\n
    ...  13) Invalid 'Run Keyword If' usage.\n\n
    ...  14) Invalid 'Run Keyword If' usage.
    Run Keyword If    False   No Operation    ELSE IF    True
    Run Keyword If    False   No Operation    ELSE IF
    Run Keyword If    False   No Operation    ELSE
    Run Keyword If    False   No Operation    ELSE IF    True    ELSE    No Operation
    Run Keyword If    False   No Operation    ELSE IF    ELSE    No Operation
    Run Keyword If    False   No Operation    ELSE IF    ELSE
    Run Keyword If    False   ELSE IF    ELSE
    Run Keyword If    False   ELSE IF
    Run Keyword If    False   ELSE
    Run Keyword If    ELSE IF    ELSE
    Run Keyword If    ELSE IF
    Run Keyword If    ELSE
    Run Keyword If    True    ELSE    No Operation
    Run Keyword If    True    ELSE

Run Keyword If with escaped or non-caps ELSE IF and ELSE
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) ${LOG GOT THREE ARGS}\n\n
    ...  2) ${LOG GOT WRONG ARGS} 5.
     Run Keyword If    expr    Log    \ELSE IF    not expr    not kw
     Run Keyword If    expr    Log    \ELSE    not kw (and not level either)
     Run Keyword If    expr    Log    else if    valid args for log
     Run Keyword If    expr    Log    else    too    many    args    again

Run Keyword If with list variable in ELSE IF and ELSE
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) Keyword 'BuiltIn.Log' expected 1 to 2 arguments, got 4.\n\n
    ...  2) Keyword 'BuiltIn.Log' expected 1 to 2 arguments, got 5.
    Run Keyword If    @{list}
    Run Keyword If    @{list}    ELSE      @{list}
    Run Keyword If    @{list}    ELSE IF   @{list}
    Run Keyword If    @{list}    ELSE IF   @{list}    ELSE    @{list}
    Run Keyword If    @{list}    Not Considered Keyword     ELSE    @{list}    XXX
    Run Keyword If    @{list}    ELSE      Log   1    2     3    4
    Run Keyword If    expr       No Operation    ELSE IF    @{list}    UK    1
    Run Keyword If    expr       No Operation    ELSE IF    @{list}    UK    1    2
    ...    ELSE IF    expr       Log    1    2    3    4    5
    Run Keyword If    expr       No Operation    ELSE IF    @{list}    No Operation
    ...  ELSE    @{list}

Test Teardown Related Run Keyword Variants
    [Documentation]  FAIL  Several failures occurred:\n\n
    ...  1) ${LOG GOT ZERO ARGS}\n\n
    ...  2) ${LOG GOT ZERO ARGS}\n\n
    ...  3) ${LOG GOT ZERO ARGS}
    Run Keyword If Test Failed  Log
    Run Keyword If Test Passed  Log
    Run Keyword If Timeout Occurred  Log


*** Keywords ***
UK  No Operation

Failing UK  Log
