*** Settings ***
Suite Setup       Check Preconditions
Library           Process
Resource          resource.txt

*** Test Cases ***
No Process Should Fail
    Run Keyword And Expect Error    No active process.   Is Process Running

Test Process Should Be Alive
    ${handle}=    Some process
    Process Should Be Running    ${handle}
    ${running} =    Is Process Running
    Should Be Equal    ${running}    ${TRUE}
    Stop some process
    Wait For Process    ${handle}
    ${running} =    Is Process Running
    Should Be Equal    ${running}    ${FALSE}
    Run Keyword And Expect Error    Process is not running.    Process Should Be Running    ${handle}

Test Process Should Be Dead
    ${handle}=    Some process
    Run Keyword And Expect Error    Process is running.    Process Should Be Stopped    ${handle}
    Stop some process
    Wait For Process    ${handle}
    Process Should Be Stopped    ${handle}

*** Keywords ***
Check Preconditions
    ${is_ok}=   Evaluate   sys.version_info >= (2,6) and sys.platform != 'cli'   sys
    Run Keyword If  not ${is_ok}    Set Suite Metadata  info   precondition_fail
    Run Keyword If  not ${is_ok}    Fail
