*** Settings ***
Resource         resource.txt
Test Teardown    Remove File    ${OUTPUT}
Test Setup       Parse XML To Test Variable    ${DEFAULT NS}    \${ROOT}    default    default2

*** Test Cases ***
Default namespace is not added to tag name
    Should Be Equal    ${ROOT.tag}    test
    ${children} =    Get Child Elements    ${root}
    Should Be Equal    ${children[0].tag}    child
    Should Be Equal    ${children[1].tag}    another
    Should Be Equal    ${children[2].tag}    back

Default namespace is not needed in xpath
    [Template]    Element Text Should Be
    ${ROOT}    default ns        xpath=child
    ${ROOT}    2nd default       xpath=another/child
    ${ROOT}    back in default   xpath=back

Default xmlns attributes are preserved
    [Template]    Element Should Have Attributes
    ${ROOT}    .    name    root    xmlns    default
    ${ROOT}    another    xmlns    default2

No extra xmlns attribute is added
    [Template]    Element Should Have Attributes
    ${ROOT}    child    id    1
    ${ROOT}    another/child
    ${ROOT}    back

Saved XML is exactly same
    Save XML    ${ROOT}    ${OUTPUT}
    ${content} =    Get File    ${OUTPUT}
    ${expected} =    Get File    ${DEFAULT NS}
    Should Be Equal    ${content}    ${expected.strip()}


*** Keywords ***
Element Should Have Attributes
    [Arguments]    ${source}    ${xpath}    @{attributes}
    ${elem} =    Get Element    ${source}    ${xpath}
    ${expected} =    Create Dictionary    @{attributes}
    Dictionaries Should Be Equal    ${elem.attrib}    ${expected}
