*** Settings ***
Resource          resource.txt
Test Template     Elements should not be equal

*** Test Cases ***
Elements should be equal
    [Template]    Element should be equal to itself
    <tag/>
    <root><tag/></root>
    <tag a="1" b="2"/>
    <root>\n<tag>some\ntext</tag>tail text</root>

Tag names are different
    <tag/>   <täg/>    Different tag name: tag != täg

Different attribute values
    <tag a="1"/>   <tag a="2"/>    Different value for attribute 'a': 1 != 2
    <tag a="1" c="3"/>   <tag b="1"/>
    ...   Different attribute names: ['a', 'c'] != ['b']

Texts are different
    <tag>some text</tag>   <tag>different</tag>  Different text: some text != different
    <tag>some text</tag>   <tag/>    Different text: some text !=${SPACE}

Different tails
    <root><tag/>tail</root>   <root><tag/>wrong</root>    Different tail text: tail != wrong
    <root><tag/>tail</root>   <root><tag/></root>    Different tail text: tail !=${SPACE}

Different number of children
    <root><tag/><tag/></root>    <root><tag/></root>   Different number of child elements: 2 != 1

Differences in child
    <root><tag><child/></tag></root>    <root><different></different>tail</root>
    ...    Different tag name: tag != different

Normalize whitespace
    [Template]    NONE
    Elements should be equal    <p>\n\tThis \ \ \ text\n<i>spaces \ has</i> also \ in\ttail!\n</p>
    ...   <p>This text <i>spaces has</i> also in tail!</p>    normalize
    Elements should not be equal    <tag>\ntext \ here\n</tag>    <tag>\twrong \ here\t</tag>
    ...   Different text: text here != wrong here    normalize

*** Keywords ***
Element should be equal to itself
    [Arguments]    ${source}
    ${xml}=    Parse XML    ${source}
    Elements Should Be Equal    ${source}    ${xml}
    Elements Should Be Equal    ${xml}    ${source}

Elements should not be equal
    [Arguments]   ${source}    ${expected}    ${error}    ${normalize}=${FALSE}
    Run Keyword and Expect Error    ${error}
    ...    Elements Should Be Equal   ${source}    ${expected}    ${normalize}
